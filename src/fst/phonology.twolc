!    =================================== !
!! # The Qʼeqchiʼ morphophonological/twolc rules file 
!    =================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-kek/blob/main/src/fst/phonology.twolc)

!! ## Alphabet, sets
Alphabet
 a b c e h i j k l m n o p q r s t u w x y z '
 A B C E H I J K L M N O P Q R S T U W X Y Z '

%{K%}:k
%{A%}:a
%{FutA%}:0
%{FutA%}:a
%{I%}:i
%{TrsA%}:a
%{TrsK%}:k
%{SetA.Sg1%}:0
%{SetA.Sg1%}:i
%{SetA.Sg1%}:n
%{SetA.Sg1%}:w
%{SetA.Sg2%}:w
%{SetA.Sg3%}:r
%{SetA.Sg3%}:x
%{SetA.Pl2%}:0
%{SetA.Pl3%}:0
%{SetA.Pl1%}:0
%{SetA.Pl1%}:a
%{Sg3I%}:0
%<:0

 ;

Sets

 Vow = a e i o u ;
 Cns = b c h j k l m n p q r s t w x y z ' ;

 !Set A where k deletes in present tense transitives
 SetA = %{SetA.Sg1%} %{SetA.Sg3%} %{SetA.Pl1%} %{Sg3I%} ;

!! ## Rules
Rules

! ! Rules for intransitive conjugations
"Present habitual vowell harmony"
%{A%}:Vx <=> n _ %{K%}: %< Vy ;
        where Vy in ( a e i o u %< )
              Vx in ( a e 0 o u a ) 
        matched ;

"Present habitual k deletion"
%{K%}:0 <=> %{A%}: _ %< [ i | %< ] ;

"Future irrealis 3rd person a elongation"
%{FutA%}:a <=> t _ %{FutA%}: %< %< ;

"Future irrealis 3rd person a elongation rule 2"
%{FutA%}:a <=> t %{FutA%}: _ %< %< ;

"Imperative, narrative past, and negative imperative i"
%{I%}:Vx <=> _ %< Vy ;
        where Vy in ( a e i o u %< )
              Vx in ( 0 0 0 0 0 i ) 
        matched ;

! ! Rules for transitive conjugations
"Transitive present habitual vowell harmony"
%{TrsA%}:Vx <=> .#. n _ %{TrsK%}: %<: ( %<: ) Vy: ;
        where Vy in ( a e i o u )
              Vx in ( a e i o u ) 
        matched ;

"Transitive present habitual k deletion"
%{TrsK%}:0 <=> .#. n %{TrsA%}: _ %<: %<: (q:) SetA: ;
! sik'+V+TV+Prs+SetB.Sg3+SetA.Sg1 n{TrsA}{TrsK}<<{SetA.Sg1}{SetA.Sg1}<sik'
!                                 n 0      0    << i         n        <sik'

"TrsA > 0 before SetB.Sg3 and SetA.Sg1"
%{TrsA%}:0 <=> .#. n _ %{TrsK%}: %<: %<: %{SetA.Sg1%}: %{SetA.Sg1%}: %<: Cns;

"TrsA > a before SetB.Sg3 and SetA.Sg3"
%{TrsA%}:a <=> .#. n _ %{TrsK%}: %<: %<: %{Sg3I%}: %{SetA.Sg3%}: %<: Cns;
! sik'+V+TV+Prs+SetB.Sg3+SetA.Sg3 n{TrsA}{TrsK}<<{Sg3I}{SetA.Sg3}<sik'
!                                 n a     0    << 0      x        <sik' 

"TrsA > a before SetB.Sg3 and SetA.Pl1"
%{TrsA%}:a <=> .#. n _ %{TrsK%}: %<: %<: q: %{SetA.Pl1%}: %<: Cns;
! sik'+V+TV+Prs+SetB.Sg3+SetA.Pl1 n{TrsA}{TrsK}<<q{SetA.Pl1}<sik'
!                                 n a     0    <<q a        <sik' 

!Set A first Person singular
"Set A first person pre-consonantal rule 1"
%{SetA.Sg1%}:i <=> _ %{SetA.Sg1%}: %<: Cns ; 

"Set A first person pre-consonantal rule 2"
%{SetA.Sg1%}:n <=> %{SetA.Sg1%}: _ %<: Cns ;

"Set A first person pre-vocalic rule 1"
%{SetA.Sg1%}:0 <=> _ %{SetA.Sg1%}: %<: Vow ; 

"Set A first person pre-vocalic rule 2"
%{SetA.Sg1%}:w <=> %{SetA.Sg1%}: _ %<: Vow ;

!Set A Second person singular
"Set A second person singular pre-vocalic"
%{SetA.Sg2%}:w <=> a a _ %<: Vow ;

!Set A Second person singular preconsonantal
"Set A second person singular pre-vocalic"
%{SetA.Sg2%}:0 <=> a a _ %<: Cns ;

!Set A third person singular
"Set A third person singular pre-vocalic"
%{SetA.Sg3%}:r <=> _ %<: Vow ;

"Set A third person singular pre-consonantal"
%{SetA.Sg3%}:x <=> _ %<: Cns ;

"i between Set B 1Sg and Set A third person singular"
%{Sg3I%}:i <=> i: n: %<: _ %{SetA.Sg3%}: ;

!Set A Second person plural
"Set A second person plural pre-vocalic"
%{SetA.Pl2%}:r <=> _ %<: Vow ;

!Set A third person plural
"Set A third person plural pre-vocalic"
%{SetA.Pl3%}:r <=> _ %<: Vow ;

!Set A third person plural
"Set A third person plural pre-consonantal"
%{SetA.Pl3%}:x <=> _ %<: Cns ;

!Set A first person plural
"Set A first person plural pre-consonantal"
%{SetA.Pl1%}:a <=> _ %<: Cns ;

!Set A first person pre-vocalic
"Set A first person plural pre-vocalic"
%{SetA.Pl1%}:0 <=> _ %<: Vow ;

!Intervocalic h
"Intervocalic h"
%<:h <=> :Vow _ :Vow ;
