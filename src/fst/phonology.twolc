!    =================================== !
!! # The Qʼeqchiʼ morphophonological/twolc rules file 
!    =================================== !

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-kek/blob/main/src/fst/phonology.twolc) 

!! ## Alphabet, sets
Alphabet
 a b c e h i j k l m n o p q r s t u w x y z '
 A B C E H I J K L M N O P Q R S T U W X Y Z '

 %{POSS.1SN%}:0
 %{POSS.2SN%}:0
 %{POSS.3SN%}:0
 %{POSS.1PL%}:0
 %{POSS.2PL%}:0
 %{POSS.3PL%}:0
 %{ERG.3PL%}:0
 %{RECPST%}:0
 %{FUT%}:0
 %{HAB%}:0
 %{REMPST%}:0
 %{K%}:k

 ;

Sets

 Vow = a e i o u ;
 Cns = b c h j k l m n p q r s t w x y z ' ;

!! ## Rules
Rules

!! ### Rules for possessive and ergative prefixes

"First Person possessive singular prevocalic form"
%{POSS.1SN%}:w <=> _ %{POSS.1SN%}: Vow ; 

"First person possessive singular preconsonantal form Rule 1"
%{POSS.1SN%}:i <=> _ %{POSS.1SN%}: Cns ;

"First person possessive singular preconsonantal form Rule 2"
%{POSS.1SN%}:n <=> %{POSS.1SN%}: _ Cns ;

"Second person possessive singular prevocalic form"
%{POSS.2SN%}:w <=> a: a: _ Vow ;

"Third person possessive singular prevocalic form"
%{POSS.3SN%}:r <=> _ Vow ;

"Third person possessive singular preconsonantal form"
%{POSS.3SN%}:x <=> _ Cns ;

"First person possessive plural preconsonantal form"
%{POSS.1PL%}:a <=> q: _ Cns ;

"Second person possessive plural prevocalic form"
%{POSS.2PL%}:r <=> e: e: _ Vow ;

"Third person possessive plural prevocalic form"
%{POSS.3PL%}:r <=> _ Vow ;

"Third person possessive plural preconsonantal form"
%{POSS.3PL%}:x <=> _ Cns ;

"Third person ergative plural preconsonantal form"
%{ERG.3PL%}:x <=> ': _ Cns ;

"Third person ergative plural prevocalic form"
%{ERG.3PL%}:r <=> ': _ Vow ;

!Rules for Recent Past tense
"Recent past before 3SN ergative preconsonantal prefix"
%{RECPST%}:i <=> x: _ %{POSS.3SN%}: Cns ;

!Rules for future tense
"Future tense before 1SN ergative prevocalic prefix Rule 1"
%{FUT%}:i <=> t: _ %{FUT%}: %{POSS.1SN%}: %{POSS.1SN%}: Vow ;

"Future tense before 1SN ergative prevocalic prefix Rule 2"
%{FUT%}:n <=> %{FUT%}: _ %{POSS.1SN%}: %{POSS.1SN%}: Vow ;

"Future tense before 1PL ergative prevocalic prefix Rule 1"
%{FUT%}:a <=> t: _ %{FUT%}: q: %{POSS.1PL%}: Vow ;

"Future tense before 1PL ergative prevocalic prefix Rule 2"
%{FUT%}:a <=> %{FUT%}: _ q: %{POSS.1PL%}: Vow ;

"Future tense before 3SN ergative prevocalic prefix Rule 1"
%{FUT%}:a <=> t: _ %{FUT%}: %{POSS.3SN%}: Vow ;

"Future tense before 3SN ergative prevocalic prefix Rule 2"
%{FUT%}:a <=> %{FUT%}: _ %{POSS.3SN%}: Vow ;

"Future tense before 3SN ergative preconsonantal prefix"
%{FUT%}:i <=> t: _ %{FUT%}: %{POSS.3SN%}: Cns ;

"Future tense before 1PL ergative preconsonantal prefix"
%{FUT%}:a <=> t: _ %{FUT%}: q: %{POSS.1PL%}: Cns ;

"Future and nonfuture suffix"
%{K%}:q <=> .#. t: %{FUT%}: %{FUT%}: ?+ _ ;

!Rules for Present habitual tense
"Present habitual before 1SN absolutive prefix"
a:0 <=> n: _ %{HAB%}: i: n: ;

"Present habitual before 1SN ergative preconsonantal prefix"
a:0 <=> n: _ %{HAB%}: %{POSS.1SN%}: %{POSS.1SN%}: Cns ;

"Present habitual before 1SN ergative prevocalic prefix Rule 1"
a:i <=> n: _ %{HAB%}: %{POSS.1SN%}: %{POSS.1SN%}: Vow ;

"Present habitual before 1SN ergative prevocalic prefix Rule 2"
%{HAB%}:n <=> n: a:i _ %{POSS.1SN%}: %{POSS.1SN%}: Vow ;

"Present habitual before 2SN absolutive prefix"
%{HAB%}:k <=> a: _ a: t: ;

"Present habitual before 2SN ergative prefixes Rule 1"
%{HAB%}:k <=> a: _ a: a: %{POSS.2SN%}: ;

"Present habitual before 2SN ergative prefixes Rule 2"
a:0 <=> n: a: %{HAB%}: a: _ %{POSS.2SN%}: ; 

"Present habitual before 1PL absolutive prefix Rule 1"
a:o <=> n: _ %{HAB%}: o: o: ;

"Present habitual before 1PL absolutive prefix Rule 2"
%{HAB%}:k <=> n: a:o _ o: o: ;

"Present habitual before 2PL absolutive prefix Rule 1"
a:e <=> n: _ %{HAB%}: e: x: ;

"Present habitual before 2PL absolutive prefix Rule 2"
%{HAB%}:k <=> n: a: _ e: x: ;

"Present habitual before 2PL ergative prefix Rule 1"
a:e <=> n: _ %{HAB%}: e: e: ;

"Present habitual before 2PL ergative prefix Rule 2"
%{HAB%}:k <=> n: a: _ e: e: ;

"Present habitual before 3PL ergative and absolutive prefixes Rule 1"
a:e <=> n: _ %{HAB%}: e: ': ;

"Present habitual before 3PL ergative prefixes Rule 2"
%{HAB%}:k <=> n: a: _ e: ': ;

!Rules for remote past tense
"Remote past before prefixes beginning in a consonant"
%{REMPST%}:i <=> k: _ Cns ;

"Remote past shorten aa to a in 2SN ergative prefixes"
a:0 <=> k: %{REMPST%}: _ a: %{POSS.2SN%}: ;

"Remote past shorten ee to e in 2PL ergative prevocalic prefixes"
e:0 <=> k: %{REMPST%}: _ e: %{POSS.2PL%}: Vow ;

"Remote past before 1SN ergative prevocalic prefixes"
%{REMPST%}:i <=> k: _ %{POSS.1SN%}: %{POSS.1SN%}: Vow ;

"Remote past before 3SN ergative prefixes"
%{REMPST%}:i <=> k: _ %{POSS.3SN%}: ;
